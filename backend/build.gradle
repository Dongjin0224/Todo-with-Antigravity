plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.epages.restdocs-api-spec' version '0.19.2'
}

group = 'com.todo'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    
    // PostgreSQL
    runtimeOnly 'org.postgresql:postgresql'
    
    // Lombok (보일러플레이트 코드 줄이기)
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    
    // 테스트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.h2database:h2'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Security & JWT
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Test
    testImplementation 'org.springframework.security:spring-security-test'

    // Spring REST Docs + restdocs-api-spec
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

// OpenAPI 3.0 스펙 생성 설정
openapi3 {
    setServer("http://localhost:8080")
    title = "Todo API"
    description = "Todo 애플리케이션 REST API 문서"
    version = "1.0.0"
    format = "json"
    outputFileNamePrefix = "openapi3"
    outputDirectory = "build/api-spec"
}

// 테스트 후 OpenAPI 스펙 생성
tasks.named('test') {
    finalizedBy 'openapi3'
}

// OpenAPI 스펙을 static 폴더로 복사
tasks.register('copyOpenApiSpec', Copy) {
    dependsOn 'openapi3'
    from("${project.buildDir}/api-spec")
    into("${project.projectDir}/src/main/resources/static/docs")
}
